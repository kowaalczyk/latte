use std::str::FromStr;
use std::collections::HashMap;

use crate::parser::ast;
use crate::parser::ast::{Keyed, LocationMeta, AstItem};
use crate::meta::Meta;
use crate::util::env;
use crate::util::env::UniqueEnv;
use crate::error::FrontendError;

use lalrpop_util::ErrorRecovery;

grammar<'err>(errors: &'err mut Vec<FrontendError<usize>>);

// --- macros ---

/// vector of 0 or more T separated by Sep
Separated<Sep, T>: Vec<T> = {
    <v:(<T> Sep)*> <e:T?> => match e {
        None => v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        }
    }
};

/// vector of 1 or more T separated by Sep
NonEmptySeparated<Sep, T>: Vec<T> = {
    <v:(<T> Sep)*> <e:T> => {
        let mut v = v;
        v.push(e);
        v
    }
};

/// wrap the item together with LocationMeta
Located<ItemT>: AstItem<ItemT, LocationMeta> = {
    <offset:@L> <item:ItemT> => {
        let meta = LocationMeta { offset };
        AstItem::new(item, meta)
    }
};

// --- top level ---

pub Program: ast::Program<LocationMeta> = {
    <topdefs:TopDef+> => {
        let mut functions: env::Env<ast::Function<LocationMeta>> = env::Env::new();
        let mut classes: env::Env<ast::Function<LocationMeta>> = env::Env::new();
        for topdef in topdefs {
            let location = topdef.get_meta();
            match topdef.item {
                ast::TopDef::Class { cls } => {
                    classes.insert_unique(cls.get_key().clone(), cls)
                        .or_else(|e| { errors.push(e); Err(()) });
                },
                ast::TopDef::Function { func } => {
                    functions.insert_unique(func.get_key().clone(), func)
                        .or_else(|e| { errors.push(e); Err(()) });
                },
                ast::TopDef::Error => (),
            };
        }
        ast::Program { functions, classes }
    },
};

TopDef: ast::TopDef<LocationMeta> = {
    <offset:@L> <ret:Type> <ident:Ident> "(" <arg_vec:Args> ")" <block:Block> => {
        match ast::FunctionItem::new(ret, ident, arg_vec, block) {
            Ok(func_item) => {
                let func = ast::FunctionItem::new(ret, ident, arg_vec, block)?;
                let meta = LocationMeta { offset };
                ast::TopDef::Function::new(func, offset)
            },
            Err(e) => {
                errors.push(e);
                ast::TopDef::Error
            }
        }
    },
    <offset:@L> "class" <ident:Ident> "{" <var_vec:ClassVar*> <method_vec:Method*> "}" => {
        let mut method_env_errors: Vec<_> = method_vec.iter().filter_map(Result::err).collect();
        let loc = LocationMeta { offset };
        if method_env_errors.is_empty() {
            let method_vec: Vec<_> = method_vec.into_iter().filter_map(Result::ok).collect();
            match ast::ClassItem::new(ident, var_vec, method_vec) {
                Ok(cls_item) => {
                    let cls = ast::Class::new(cls_item, loc);
                    ast::TopDef::Class { cls }
                },
                Err(mut e) => {
                    errors.append(&mut e);
                    ast::TopDef::Error
                }
            }
        } else {
            errors.append(&mut method_env_errors);
            ast::TopDef::Error
        }
    },
    <offset:@L> "class" <ident:Ident> "extends" <parent:Ident> "{" <var_vec:ClassVar*> <method_vec:Method*> "}" => {
        let mut method_env_errors: Vec<_> = method_vec.iter().filter_map(Result::err).collect();
        let loc = LocationMeta { offset };
        if method_env_errors.is_empty() {
            let method_vec: Vec<_> = method_vec.into_iter().filter_map(Result::ok).collect();
            match ast::ClassItem::new(ident, var_vec, method_vec) {
                Ok(mut cls_item) => {
                    let cls = ast::Class::new(cls_item.with_parent(&parent), loc);
                    ast::TopDef::Class { cls }
                },
                Err(mut e) => {
                    errors.append(&mut e);
                    ast::TopDef::Error
                }
            }
        } else {
            errors.append(&mut method_env_errors);
            ast::TopDef::Error
        }
    },
    ! => {
        errors.push(FrontendError::from(<>));
        ast::TopDef::Error
    },
};

ClassVar = Located<ClassVarItem>;
ClassVarItem: ast::ClassVarItem<LocationMeta> = {
    <t:Type> <ident:Ident> ";" => {
        ast::ClassVarItem { t, ident, default: Option::None }
    },
    <t:Type> <ident:Ident> "=" <default:Expr0> ";" => {
        ast::ClassVarItem { t, ident, default: Option::Some(default) }
    },
};

Method: Result<ast::Function<LocationMeta>, FrontendError<LocationMeta>> = {
    <offset:@L> <ret:Type> <ident:Ident> "(" <args:Args> ")" <block:Block> => {
        // not to be confused with ast::TopDef::Function 
        let func = ast::FunctionItem::new(ret, ident, args, block)?;
        let meta = LocationMeta { offset };
        Ok(ast::Function::new(func, meta))
    },
};

Arg = Located<ArgItem>;
ArgItem: ast::ArgItem = {
    <t:Type> <ident:Ident> => ast::ArgItem { t, ident },
};

Args = Separated<",", Arg>;


// --- blocks ---

Block = Located<BlockItem>;
BlockItem: ast::BlockItem<LocationMeta> = {
    "{" <stmts:Statement*> "}" => ast::BlockItem { stmts },
};

// --- statements ---

Statement = StatementADI<"I">;
StatementADI<I> = Located<StatementKindADI<I>>;
// StatementKind = StatementKindADI<"I">;
StatementKindADI<I>: Box<ast::StatementKind<LocationMeta>> = {
    <block:Block> => {
        Box::new(ast::StatementKind::Block { block })
    },
    ";" => Box::new(ast::StatementKind::Empty),
    <t:Type> <items:DeclItems> ";" => {
        Box::new(ast::StatementKind::Decl { t, items })
    },
    <r:Reference> "=" <expr:Expr0> ";" => {
        Box::new(ast::StatementKind::Ass { r, expr })
    },
    <r:Reference> "++" ";" => {
        Box::new(ast::StatementKind::Mut { r, op: ast::StatementOp::Increment })
    },
    <r:Reference> "--" ";" => {
        Box::new(ast::StatementKind::Mut { r, op: ast::StatementOp::Decrement })
    },
    "return" <e:Expr0> ";" => {
        Box::new(ast::StatementKind::Return { expr: Option::Some(e) })
    },
    "return" ";" => {
        Box::new(ast::StatementKind::Return { expr: Option::None })
    },
    // we only allow dangling if when template parameter I != "" (I == "I")
    "if" "(" <expr:Expr0> ")" <stmt:StatementADI<"I">> if I != "" => {
        Box::new(ast::StatementKind::Cond { expr, stmt })
    },
    "if" "(" <expr:Expr0> ")" <stmt_true:StatementADI<"">> "else" <stmt_false:StatementADI<I>> => {
        Box::new(ast::StatementKind::CondElse { expr, stmt_true, stmt_false })
    },
    "while" "(" <expr:Expr0> ")" <stmt:StatementADI<I>> => {
        Box::new(ast::StatementKind::While { expr, stmt })
    },
    "for" "(" <t:Type> <ident:Ident> ":" <arr:Expr0> ")" <stmt:StatementADI<I>> => {
        Box::new(ast::StatementKind::For { t, ident, arr, stmt })
    },
    <expr:Expr0> ";" => Box::new(ast::StatementKind::Expr { expr }),
    ! => {
        errors.push(FrontendError::from(<>));
        Box::new(ast::StatementKind::Error)
    },
};

DeclItem = Located<DeclItemKind>;
DeclItemKind: ast::DeclItem<LocationMeta> = {
    <ident:Ident> => ast::DeclItemKind::NoInit { ident },
    <ident:Ident> "=" <val:Expr0> => ast::DeclItemKind::Init { ident, val },
};

DeclItems = NonEmptySeparated<",", DeclItem>;

// --- types ---

Type: ast::Type = {
    "int" => ast::Type::Int,
    "string" => ast::Type::Str,
    "boolean" => ast::Type::Bool,
    "void" => ast::Type::Void,
    <ident:Ident> => ast::Type::Class { ident },
    <t:Type> "[]" => ast::Type::Array { item_t: Box::new(t) },
};

Types = Separated<",", Type>;

// --- expressions ---

Expr0 = Located<Expr0Kind>;
Expr0Kind: Box<ast::ExpressionKind<LocationMeta>> = {
    <l:Expr1> "||" <r:Expr0> => {
        Box::new(ast::ExpressionKind::Binary { left: l, op: ast::BinaryOperator::Or, right: r })
    },
    <e:Expr1Kind> => (<>),
};

Expr1 = Located<Expr1Kind>;
Expr1Kind: Box<ast::ExpressionKind<LocationMeta>> = {
    <l:Expr2> "&&" <r:Expr1> => {
        Box::new(ast::ExpressionKind::Binary { left: l, op: ast::BinaryOperator::And, right: r })
    },
    <e:Expr2Kind> => (<>),
};

Expr2 = Located<Expr2Kind>;
Expr2Kind: Box<ast::ExpressionKind<LocationMeta>> = {
    <left:Expr2> <op:RelOp> <right:Expr3> => {
        Box::new(ast::ExpressionKind::Binary { left, op, right })
    },
    <e:Expr3Kind> => (<>),
};

RelOp: ast::BinaryOperator = {
    "<" => ast::BinaryOperator::Less,
    "<=" => ast::BinaryOperator::Less,
    "==" => ast::BinaryOperator::Equal,
    "!=" => ast::BinaryOperator::NotEqual,
    ">=" => ast::BinaryOperator::GreaterEqual,
    ">" => ast::BinaryOperator::Greater,
};

Expr3 = Located<Expr3Kind>;
Expr3Kind: Box<ast::ExpressionKind<LocationMeta>> = {
    <l:Expr3> <op:AddOp> <r:Expr4> => {
        Box::new(ast::ExpressionKind::Binary { left: l, op: op, right: r })
    },
    <e:Expr4Kind> => (<>),
};

AddOp: ast::BinaryOperator = {
    "+" => ast::BinaryOperator::Plus,
    "-" => ast::BinaryOperator::Minus,
};

Expr4 = Located<Expr4Kind>;
Expr4Kind: Box<ast::ExpressionKind<LocationMeta>> = {
    <l:Expr4> <op:MulOp> <r:Expr5> => {
        Box::new(ast::ExpressionKind::Binary { left: l, op: op, right: r })
    },
    <e:Expr5Kind> => (<>),
};

MulOp: ast::BinaryOperator = {
    "*" => ast::BinaryOperator::Times,
    "/" => ast::BinaryOperator::Divide,
    "%" => ast::BinaryOperator::Modulo,
};

Expr5 = Located<Expr5Kind>;
Expr5Kind: Box<ast::ExpressionKind<LocationMeta>> = {
    "-" <e:Expr6> => Box::new(ast::ExpressionKind::Unary { op: ast::UnaryOperator::Neg, arg: e }),
    "!" <e:Expr6> => Box::new(ast::ExpressionKind::Unary { op: ast::UnaryOperator::Not, arg: e }),
    <e:Expr6Kind> => (<>),
};

Expr6 = Located<Expr6Kind>;
Expr6Kind: Box<ast::ExpressionKind<LocationMeta>> = {
    "new" <t:Type> => {
        Box::new(ast::ExpressionKind::InitDefault { t })
    },
    "new" <item_t:Type> "[" <size:Expr0> "]" => {
        Box::new(ast::ExpressionKind::InitArr { 
            t: ast::Type::Array { item_t: Box::new(item_t) }, 
            size 
        })
    },
    <r:Reference> "(" <args:Exprs> ")" => {
        Box::new(ast::ExpressionKind::App { r, args })
    },
    <r:Reference> => {
        Box::new(ast::ExpressionKind::Reference { r })
    },
    <val:Num> => Box::new(ast::ExpressionKind::LitInt { val }),
    "true" => Box::new(ast::ExpressionKind::LitBool { val: true }),
    "false" => Box::new(ast::ExpressionKind::LitBool { val: false }),
    "null" => Box::new(ast::ExpressionKind::LitNull),
    // TODO: Unescape characters from the string (!!!)
    r#""[^"]*""# => Box::new(ast::ExpressionKind::LitStr { val: String::from(<>) }),
    // nested expression conflicts with type cast, so we only implement the possibility of casting null
    "(" <t:Type> <expr:NullCast> => Box::new(ast::ExpressionKind::Cast { t, expr }),
    // https://stackoverflow.com/questions/27448189/bison-reduce-reduce-conflict-with-casting-and-expression-parentheses
    // "(" <t:Type> ")" <expr:Expr6> => Box::new(ast::Expression::Cast { t, expr }),
    "(" <e:Expr0Kind> ")" => (<>),
};

Exprs = Separated<",", Expr0>;

// --- references ---

Reference = Located<ReferenceKind>;
ReferenceKind: ast::ReferenceKind<LocationMeta> = {
    <ident:Ident> => ast::ReferenceKind::Ident { ident },
    <obj:Ident> "." <field:Ident> => ast::ReferenceKind::Object { obj, field },
    "self" "." <field:Ident> => ast::ReferenceKind::ObjectSelf { field },
    <arr:Ident> "[" <idx:Expr0> "]" => ast::ReferenceKind::Array { arr, idx },
    <ident:Ident> "." "length" => ast::ReferenceKind::ArrayLen { ident },
};

// --- special null cast ---

NullCast = Located<NullCastKind>;
NullCastKind: Box<ast::ExpressionKind<LocationMeta>> = {
    ")null" => Box::new(ast::ExpressionKind::LitNull),
};

// --- literals ---

Ident: String = {
    r"[a-zA-Z_][a-zA-Z_0-9]*" => String::from(<>),
};

Num: i32 = {
    r"[0-9]+" => i32::from_str(<>).unwrap(),  // no negative numbers, just as the LBNF Integer
};
